rem Asalto y castigo
rem para QL, en SuperBASIC.
rem Versión 00-20110405

rem Copyright (C) 2011 Marcos Cruz (programandala.net)
rem Licencia:
rem http://programandala.net/licencia
rem Página del programa:
rem http://programandala.net/es.programa.asalto_y_castigo

rem Autor de las versiones originales
rem para ZX Spectrum, Amstrad CPC y Blassic:
rem Baltasar el Arquero
rem http://caad.es/baltasarq/
 
rem --------------------------------------------
rem Main

ayc

defproc ayc

  init_ql
  credits
  init_vars
  read_data
  end_of_scene
  intro
  action_look
  rep game
    plot
    let t$=accept$
    parser
    print object,complement
    print action,obj
    do_action
  endrep

enddef ayc

rem --------------------------------------------
rem Plot

deffn location_plot$

  if location=11 or location=38 or location=44:let thing_data(15,1)=location

  if location=16:ret "En la distancia, por entre los resquicios de las rocas, y allende el canal de agua, los sajones tratan de buscar la salida que encontraste por casualidad."
  if location<10 and location_exit(8,1):ret "Tus hombres siguen tus pasos."
  if thing_data(2,1)<>255 and thing_data(16,2)=0 and (location=46 or sig=1)
    let thing_data(2,1)=location
    ret "Tu benefactor te sigue, esperanzado."
  endif
  if location=28:let location_exit(28,3)=0:ret ""
  if location=31
    if location_exit(31,1)
      ret "Las rocas yacen desmoronadas a lo largo del pasaje."
    else
      ret "Las rocas bloquean el camino."
    endif
  endif
  ret ""

enddef

defproc plot

  if location=8 and location_exit(8,1)
    let location_exit(8,1)=0
    let trSax=1
    narrate "Una partida sajona aparece por el este. Para cuando te vuelves al norte, ya no te queda ninguna duda: era una trampa."
    end_of_scene
    narrate "En el estrecho paso es posible resistir, aunque por desgracia sus efectivos son muy superiores a los tuyos."
    wait
    cls_
    narrate "Tus oficiales te conminan a huir."
    speak "Capturando a un general britano, ganan doblemente."
    narrate "Sabes que es cierto, y te duele."
    wait

  else

    if trSax>0 and location<12
      let trSax=trSax+1
      narrate "No sabes cuanto tiempo te queda..."
    endif
    if trSax>10 and location<12
      narrate "Los sajones te capturan. Su general, sonriendo ampliamente, dice:"
      speak "Bien, bien... Del gran Ulfius podremos sacar una buena ventaja."
      end_of_scene
      let action=17:do_action
      ret
    endif
    if location<10 and not location_exit(8,1)
      narrate "Tus hombres luchan con denuedo contra los sajones."
    endif
    if location>19 and not is_here(4)
      narrate "Ante la reinante e intimidante oscuridad, retrocedes a donde puedes ver."
      end_of_scene
      let location=17:action_look
      ret
    endif
    if location=51
      narrate "Agotado, das parte en el castillo de tu llegada y de lo que ha pasado."
      wait
      narrate "Pides audiencia al rey, Uther Pendragon."
      end_of_scene
      cls_
      speak "El rey, te indica el valido, ha ordenado que no se le moleste, pues sufre una amarga tristeza."
      wait
      narrate "No puedes entenderlo. El rey, tu amigo."
      wait
      narrate "Agotado, decepcionado, apesadumbrado, decides ir a dormir a tu casa. Es lo poco que puedes hacer."
      end_of_scene
      narrate "Te has ganado un buen descanso"
      let action=17:do_action
    endif
  endif

enddef

rem --------------------------------------------
rem Actions

defproc do_action

  rem Action dispatcher

  if action=-1:narrate "No conozco ese verbo.":ret

  if action_type(action)=1 and object=-1:_debug 7010:narrate not_seen$:ret
  if action_type(action)=2 and (object=-1 or complement=-1):_debug 7015:narrate not_seen$:ret
  if action_type(action)>0:if not is_here(object):_debug 7020:narrate not_seen$:ret
  if action_type(action)=2 and complement<>-1:if not is_here(complement):_debug 7030:narrate not_seen$:ret

  sel on action
    =17:if yes("¿Quieres volver a intentarlo?"):run:else:cls_:stop
    =13:action_look
    =10
      if object=-1:action_inventory:ret
      if is_here(object)
        narrate thing_description$(object)
      endif
        narrate not_seen$
      endif
    =14:action_move 1
    =16:action_move 2
    =9:action_move 3
    =15:action_move 4
    =3:action_move 5
    =1:action_move 6
    =5:narrate help$:end_of_scene
    =7:action_take
    =8:action_drop
    =6:action_swim
    =4:action_brake
    =11:action_speak
    =18:action_fling
    =2:action_open
    =12:action_insert
    =remainder:narrate "No puedes hacer eso."
  endsel

enddef do_action

defproc action_swim
  if location=11
    let location=12
    narrate "Caes hacia el fondo por el peso de tu coraza. Como puedes, te desprendes de ella y buceas, pensando en avanzar, aunque perdido."
    end_of_scene
    narrate "Consigues emerger, si bien en un sitio desconocido de la caverna..."
    end_of_scene
    action_look
  else
    narrate "No tiene sentido nadar ahora."
  endif
enddef

defproc action_open

  if location=47
    if is_here(16)
      narrate "El candado bloquea la puerta."
    else
      narrate "La puerta se abre, rechinando, mientras hiedras y hierbas se rompen en su trazado."
      wait
      narrate "Ambrosio, alegre, se despide de ti."
      speak "Estoy seguro de que volveremos a vernos"&r_quote$&", dice."
      narrate "Se ha ido."
      let thing_data(2,1)=255
      let thing_data(16,1)=255
      let thing_description$(19)="Entreabierta."
      let location_exit(47,4)=48
    endif
  else
    narrate "No hay nada que abrir ahora."
  endif

enddef

defproc action_drop

  sel on object
    =10:if location<12:narrate "No, es lo que queda de tu padre.":ret
    =4:narrate "No, sin luz es imposible moverse por la caverna.":ret
  endsel
  if thing_data(object,1)=0
    let thing_data(object,1)=location
    narrate "Hecho."
  else
    narrate "No llevas eso contigo."
  endif
enddef

defproc action_take
  if thing_data(object,1)=0:narrate "Pero si ya lo tienes...":ret
  if object=16 and thing_data(object,2)=1:narrate "Ambrosio la retiene consigo.":ret
  if thing_data(object,2)<>0:narrate "No es algo que se pueda coger.":ret
  let thing_data(object,1)=0:narrate "Hecho."
enddef

defproc action_brake

  loc no_tool_specified,tool

  let no_tool_specified=0
  if complement=-1
    let complement=10
    let no_tool_specified=1
  endif
  let tool=complement

  rem temp structure!!!
  rep not_with_that

  if object=22 and complement=10 and cortado=0 and no_tool_specified<>1 and is_here(tool):narrate "Sabes que el resultado es la hoja de tu espada, mellada. No.":ret
  if object=22 and complement=17 and cortado=0 and is_here(tool):let cortado=1:narrate "Con el pedernal, recortas la punta del tronco.":ret
  if object=22 and cortado=0 then narrate "El problema es encontrar la herramienta adecuada para hacerlo.":ret
  if object=22 and cortado=1 then narrate "La punta ya es lo suficientemente afilada.":ret

  if object=6 and ((is_here(tool) and complement=10) or no_tool_specified=1):let thing_data(6,1)=255:let thing_data(11,1)=0:let thing_data(12,1)=0:let thing_data(23,1)=0:narrate "Rasgas la capa, como buenamente puedes.":ret
  if object=6:exit not_with_that

  if object=20 and complement=22 and is_here(tool) and no_tool_specified=0 and cortado=1:let thing_data(20,1)=255:let location_exit(31,1)=32:narrate "Haciendo palanca, consigues desencajar una, y el resto caen por su propio peso.":ret
  if object=20 and complement=22 and is_here(tool) and no_tool_specified=0:narrate "Lo intentas con el tronco, pero la punta es demasiado gruesa, y no penetra entre los resquicios de las rocas.":ret
  if object=20 and complement=10:narrate "Tu espada no hace nada, ni, por descontado, tus manos desnudas.":ret
  if not is_here(tool):narrate "En cualquier caso, no con las manos desnudas.":ret
  if object=20 and complement=22:let thing_data(20,1)=255:let location_exit(31,1)=32:narrate "Haciendo palanca, consigues desencajar una, y el resto caen por su propio peso.":ret

  if object=17 and complement=10 and is_here(tool)
    let thing_data(3,1)=255:let thing_data(4,1)=location:let thing$(3)="amorfa":let thing$(4)="antorcha":narrate "Poderosas chispas salen del choque entre espada y pedernal, encendiendo la antorcha.":ret
  endif
  if object=17 and complement=10:narrate "Ante el potente choque con la espada, poderosas chispas saltan en todas direcciones.":ret
  if object=17:exit not_with_that

  if object=21 and complement=10:narrate "Ante los amenazadores tajos, la serpiente huye.":let location_exit(43,2)=44:let thing_data(21,1)=255:ret
  if object=21:exit not_with_that

  rem endsel

  narrate "Eso no es lo adecuado ahora.":ret

  endrep not_with_that

  narrate "Con eso no..."

enddef

defproc action_speak

  sel on object
    =2:talk_to_ambrosio
    =13:talk_to_the_man
    =remainder:narrate "No tiene sentido hablar con eso."
  endsel

enddef

defproc talk_to_ambrosio

  if thing_data(2,1)=19

    let thing_data(3,1)=0
    let thing_data(17,1)=0
    let thing_data(2,1)=46
    speak "Hola, buen hombre."
    speak "Hola, Ulfius. Mi nombre es Ambrosio."
    end_of_scene
    narrate "Por primera vez, Ulfius se sienta y cuenta a Ambrosio todo lo que ha pasado. y tras tanto acontecido, llora desconsoladamente."
    wait
    cls_
    narrate "Ambrosio le propone un trato, que acepta: por ayudarle a salir de la cueva, objetos, vitales para la empresa, le son entregados."
    wait
    speak "Bien, Ambrosio, emprendamos la marcha."
    narrate "Ulfius se da la vuelta para ver si Ambrosio le sigue, pero... ha desaparecido."
    wait
    narrate "Ulfius piensa entonces en el hecho curioso de que supiera su nombre."
    end_of_scene

  else

    sel on location
      =46
        if not sig
          speak "La llave, Ambrosio, estaba ya en tu poder. Y es obvio que conocéis un camino más corto."
          speak "Estoy atrapado en la cueva debido a magia de maligno poder. En cuanto al camino, vos debéis hacer el vuestro, verlo todo con vuestros ojos."
          narrate "Ulfius sacude la cabeza."
          speak "No lo entiendo, la verdad."
        endif
      =45,46,47
        speak "Por favor, Ulfius, cumple tu promesa. Toma la llave en tu mano y abre la puerta de la cueva."
        let thing_data(16,1)=0
        let thing_data(16,2)=0
        let sig=1
    endsel

  endif

enddef

defproc talk_to_the_man

  if not talked_to_the_man
    speak "Me llamo Ulfius y..."
    narrate "El hombre asiente, impaciente."
    speak "Somos refugiados de la gran guerra. Buscamos la paz."
    let talked_to_the_man=1
  endif
  let obj=18
  if is_here(obj)
    let thing_data(18,1)=18
    narrate "El hombre se irrita."
    speak "No podemos permitiros huir con la piedra del druida."
    narrate "Hace un gesto..."
    speak "La piedra debe devolverse a su lugar de encierro."
    narrate "Un hombre te arrebata la piedra y se la lleva."
  else
    let obj=10
    if is_here(obj)
      narrate "El hombre se enfurece, y alza su mano indicando al norte."
      speak "Nadie portando armas puede pasar."
    else
      let location_exit(28,3)=29
      narrate "El hombre, calmado, indica hacia el este y habla:"
      speak "Si vienes en paz, puedes ir en paz."
      narrate "Todos se apartan y permiten ahora el paso al este."
    endif
  endif

enddef

defproc action_fling

  sel on location
    =19 
      if (object=10 or object=18) and talked_to_the_man
        let thing_data(object,1)=39
        narrate "La corriente lo arrastra fuertemente, hasta perderlo de vista."
      else
        narrate "No quieres perder eso."
      endif
    =13,16
      narrate "No hay suficiente profundidad."
    =remainder:
      narrate "No tiene sentido tirar nada ahora."
  endsel

enddef

defproc action_insert

  sel on complement
    =14
      sel on object
        =9,18
          let thing_data(object,1)=255
          narrate "Encaja. Metido. Desaparece en su interior."
          if thing_data(18,1)=255 and thing_data(9,1)=255
            let location_exit(41,2)=42
            narrate "La gran roca se desplaza y deja el paso libre."
          endif
        =remainder
          narrate "No encaja."
      endsel
    =remainder
      narrate "No tiene sentido meter nada en eso."
  endsel

enddef

defproc action_move(direction)

  rem formerly if x>0, check!!!
  if location_exit(location,direction)
    let location=location_exit(location,direction)
    action_look
  else
    narrate "No es posible."
  endif

enddef

defproc action_inventory

  loc i,t$
  let t$=""
  for i=1 to things
    if thing_data(i,1)=0:let t$=t$&"  - "&thing$(i)&nl$
  endfor i
  if len(t$)
    print "Inventario:"\\t$:pause
  else
    narrate "No llevas nada contigo."
  endif

enddef

def proc action_look
  loc message$
  cls_
  describe h$(location) 
  let message$=location_plot$()
  if len(message$):narrate message$
  list_present_things 
enddef

rem --------------------------------------------
rem Parser

defproc parser

  let m$="":let first=1:let last=len(t$)
  let action=-1:let object=-1:let complement=-1

  rep find_first
    if not(first<=last and t$(first)=" "):exit find_first
    let first=first+1
  endrep find_first

  rep find_last
    if not(last>1 and t$(last)=" "):exit find_last
    let last=last-1
  endrep find_last

  rep loopx1
    rem todo all this is not needed because a special input will be used!!!
    if first>last:exit loopx1
    let chrAsc=code(t$(first))
    if chrAsc>=chrA and chrAsc<=chrZ:let chrAsc=diff+chrAsc
    if chrAsc<>code(" ")
      let m$=m$&chr$(chrAsc)
    else

      xxx0
      if complement<>-1:ret
      if t$(first)=" ":let first=first+1:ret
      next loopx1:exit loopx1

    endif

    let first=first+1
  endrep loopx1

  xxx0

enddef 

defproc xxx0:rem rename!!!

  rem todo use m$ instr " word1 word2 etc " !!!
  if m$<>"con" and m$<>"de" and m$<>"del" and m$<>"el" and m$<>"la" and m$<>"los" and m$<>"las" and m$<>"a" and m$<>"al" and m$<>"por" and m$<>"en" and m$<>"y"

    if action=-1
      if m$(len(m$))="r":let m$=m$(1 to len(m$)-1)
      locVerbo
    else
      if object=-1
        locObj:let object=obj
      else
        if complement=-1:locObj:let complement=obj
      endif
    endif
  endif
  let m$=""

enddef

defproc locVerbo

  let action=-1
  let n=len(m$)
  if not n
    if m$="e":let action=9:ret
    if m$="m":let action=13:ret
  endif
  if n>2:if m$(1 to 3)="nad":let action=6:ret
  if n>6:if m$(1 to 6)="recort":let action=4:ret
  if n>4:if m$(1 to 4)="afil":let action=4:ret
  if n>3:if m$(1 to 3)="mat":let action=4:ret
  if m$="examinate" or m$="examinarte":let action=10:ret
  if m$="fin":let action=17:ret
  if n>4:if m$(1 to 4)="cort":let action=4:ret
  if n>5:if m$(1 to 5)="empuj":let action=4:ret
  if n>5:if m$(1 to 5)="golpe":let action=4:ret
  if n>4:if m$(1 to 4)="romp":let action=4:ret
  if n>7:let n=7:let m$=m$(1 to 7)
  let i=1:let f=actions+1:let m=2
  rep what_for:rem rename!!!
    let m=int((i+f)/2)
    if action$(m,1 to n)=m$
      let action=m
      exit what_for
    endif
    if i=m:exit what_for
    if action$(m)>m$
      let f=m
      next what_for
    endif
    let i=m
  endrep what_for

enddef

defproc locObj:rem todo must be a function!!!

  if m$="anciano" or m$="jefe":let obj=13:ret
  let n=len(m$):let obj=-1:if n=0:ret
  if n>9:let n=9:let m$=m$(1 to 9)
  if n>2:if m$(1 to 3)="ojo":let obj=14:ret
  if n>6:if m$(1 to 7)="agujero":let obj=14:ret
  let i=1:let f=things+1

  rep xxx1
    let m=int((i+f)/2)
    if thing$(m,1 to n)=m$:let obj=m:ret
    if m=i:ret
    if thing$(m)>m$:let f=m:next xxx1
    let i=m
  endrep xxx1

enddef

deffn is_here(thing)
  if thing=-1:ret 0:rem to prevent an error. not sur if thing could be -1, but it was!
  ret thing_data(thing,1)=0 or thing_data(thing,1)=location
enddef

defproc list_present_things
  loc n,t$
  let t$="":rem objsLoc
  for n=1 to things
    if thing_data(n,1)=location and thing_data(n,2)=2
      let t$=t$&"  - "&chr$(code(thing$(n,1))-diff)&thing$(n,2 to)&nl$
      next n:exit n
    endif
    if thing_data(n,1)=location
      let t$=t$&"  - "&thing$(n)&nl$
    endif
  endfor n
  if len(t$):ink light_grey:print "Puedes ver:"&nl$&t$
enddef

rem --------------------------------------------
rem Input

deffn yes(question$)
  loc answer$
  print question$!"(S/N)"!;
  rep answer
    let answer$=inkey$
    if answer$ instr "sn":exit answer
  end rep answer
  ret answer$ instr "s"
enddef

defproc end_of_scene:rem rename!!!
  ink dark_green
  print "..."\\
  ink light_grey:rem not needed?!!!
  wait
enddef

defproc wait:rem set maximum time!!!
  rep dont_press_key:if inkey$="":exit dont_press_key
  rep press_key:if inkey$<>"":exit press_key
enddef

deffn accept$
  loc command$
  ink yellow
  rep valid_command
    input \"> ";command$
    if command$<>"":exit valid_command
  endrep valid_command
  ink light_grey
  ret command$
enddef

rem --------------------------------------------
rem Screen and text output

defproc cls_
  ink light_grey
  cls
enddef

defproc speak(quote$)

  loc last
  ink yellow
  if r_quote$ instr quote$
    rem A closing "»" in the text means the actual quote is closed before the end of quote$.
    rem This manages quotes like "«Yes», he said.".
    type l_quote$&quote$
  else
    let last=len(quote$)
    if quote$(last)="." and quote$(last-1)<>"."
      type l_quot$&quote$(to last-1)&r_quote$&'.'
    else
      type l_quote$&quote$(to last)&r_quote$
    endif
  endif

enddef

defproc describe(txt$)
  ink dark_cyan:type txt$
enddef

defproc narrate(txt$)
  ink light_grey:type txt$
enddef

defproc type(txt$)

  local text$,first,last

  if len(txt$)
    let text$=txt$&" "
    let first=1
    for last=1 to len(text$)
      if text$(last)=" "
        print #ow,!text$(first to last-1);
        let first=last+1
      endif
    endfor last
  endif
  print\\

enddef

rem --------------------------------------------
rem About

defproc credits
  cls_
  ink light_red:print "Asalto y castigo"
  ink dark_cyan
  print \"Por Baltasar el Arquero"
  print "http://caad.es/baltasarq/"
  print \\"Adaptada a SuperBASIC por:"
  print \"Marcos Cruz (programandala.net), 2011"
  ink light_grey
  print\"C.A.A.D."\"http://www.caad.es/"\\\
enddef

defproc intro
  cls_
  narrate "El sol despunta de entre la niebla, haciendo humear los tejados de paja."
  wait
  narrate "Piensas en el encargo realizado por Uther Pendragon. Atacar una aldea tranquila, aunque sea una llena de sajones, no te llena de orgullo."
  wait
  narrate "Los hombres se ciernen sobre la aldea, y la destruyen. No hubo tropas enemigas, ni honor en la batalla."
  end_of_scene
  cls_
  speak "Sire Ulfius, la batalla ha terminado."
  narrate "Lentamente, das la orden de volver a casa. Los oficiales detienen como pueden el saqueo."
  end_of_scene
enddef

rem --------------------------------------------
rem Init

defproc init_ql
  mode 8:paper 0:ink light_grey:csize 3,1:cls
  rem original Blassic/CPC colors.
  rem let black=0:let dark_blue=1:let dark_green=2:let dark_cyan=3:let red=4:let purple=5:let brown=6:let light_grey=7:let dark_grey=8:let light_blue=9:let light_green=10:let light_cyan=11:let light_red=12:let light_purple=13:let yellow=14:let white=15
  rem QL colours in mode 8:
  let black=0
  let dark_blue=1
  let dark_green=4
  let dark_cyan=5
  let red=2
  let purple=3
  let brown=2
  let light_grey=7
  let dark_grey=7
  let light_blue=5
  let light_green=4
  let light_cyan=5
  let light_red=2
  let light_purple=3
  let yellow=6
  let white=7
  let nl$=chr$(10)
enddef


defproc init_vars

  rem Constants
  let l_quote$="«":rem castilian left quote
  let r_quote$="»":rem castilian right quote

  let location=15:rem tmp!!! must be 1

  rem Flags:
  let sig=0:rem what for?!!!
  let trSax=0
  let talked_to_the_man=0
  let cortado=0

  let not_seen$="No lo veo, o no es importante."
  let chrA=code("A")
  let chrZ=code("Z")
  let diff=abs(code("a")-chrA):rem used to change the letter case, todo improve!!!
  let help$="Direcciones: n(orte), s(ur), e(ste), o(este), arriba, abajo."&nl$&"(m)irar redescribe un lugar."&nl$&"(ex)aminar permite examinar un objeto, o en su defecto a ti."&nl$&"Se puede cortar, nadar, atacar, empujar, golpear, coger, dejar, y tirar algo."

enddef

rem --------------------------------------------
rem Data

defproc read_data

  loc i,j

  restore

  rem locs
  let locations=51
  dim h$(locations,255)
  for i=1 to locations
    read h$(i)
  endfor i

  rem directions
  let directions=6
  dim location_exit(locations,directions)
  for i=1 to locations
    for j=1 to directions
      read location_exit(n,j)
    endfor j
  endfor i

  let things=23
  dim thing$(things,9)
  for i=1 to things
    read thing$(i)
  endfor i

  dim thing_description$(things,128)
  for i=1 to things
    read thing_description$(i)
  endfor i

  rem atrthings
  dim thing_data(things,2)
  for i=1 to things
    for j=1 to 2
      read thing_data(i,j)
    endfor j
  endfor i

  rem actions
  let actions=18
  dim action$(actions,7)
  dim action_type(actions)
  for i=1 to actions
    read action$(i),action_type(i)
  endfor i

enddef

data "Aldea Sajona. No ha quedado nada en pie, ni piedra sobre piedra. El entorno es desolador. Solo resta volver al sur, a casa."
data "Sobre la colina, casi sobre la niebla de la aldea sajona arrasada al norte, a tus pies. El camino desciende hacia el oeste."
data "Camino entre colinas. El camino avanza por el valle, desde la parte alta, al este, a una zona harto boscosa, al oeste."
data "Cruce de caminos. Una senda parte al oeste, a la sierra por el paso del perro, y otra hacia el norte, por un frondoso bosque que la rodea."
data "Desde la linde, al sur, hacia el oeste se extiende frondoso el bosque que rodea la sierra. La salida se abre hacia el sur."
data "Bosque. Jirones de niebla se enzarcen en frondosas ramas y arbustos. La senda serpentea entre raices, de un luminoso este al oeste."
data "Paso del perro. Abruptamente, del bosque se pasa a un estrecho camino entre altas rocas. El inquietante desfiladero tuerce de este a sur."
data "Entrada a la cueva. El paso entre el desfiladero sigue de norte a este. La entrada a una cueva se abre al sur en la pared de roca."
data "Derrumbe. El camino desciende hacia la agreste sierra, al oeste, desde los verdes valles al este. Pero un gran derrumbe bloquea la sierra."
data "Gruta de entrada. El estrecho paso se adentra hacia el oeste, desde la boca, al norte. "
data "Gran lago. Una gran estancia alberga un lago de profundas e iridiscentes aguas, debido a la luz exterior. No hay otra salida que el este."
data "Salida del paso secreto. Una gran estancia se abre hacia el oeste, y se estrecha hasta morir, al este, en una parte de agua."
data "Puente semipodrido. La sala se abre en semioscuridad a un puente cubierto de podredumbre sobre el lecho de un canal, de este a oeste."
data "Recodo de la cueva. La iridiscente cueva gira de este a sur."
data "Pasaje arenoso. La gruta desciende de norte a sur sobre un lecho arenoso. Al este, un agujero del que llega claridad."
data "Pasaje del agua. Como un acueducto, el agua baja con gran fuerza de norte a este, aunque la salida practicable es la del oeste."
data "Estalactitas. Muchas estalactitas se agrupan encima de tu cabeza, y se abren cual arco de entrada hacia el este y sur."
data "Puente de piedra. Un arco de piedra se eleva, cual puente sobre la oscuridad, de este a oeste. En su mitad, un altar."
data "Recodo arenoso del canal. La furiosa corriente, de norte a este, impide el paso, excepto al oeste. Al fondo, se oye un gran estruendo."
data "Un tramo de cueva estrecho te permite avanzar hacia el norte y el sur; un pasaje surge al este."
data "Un tramo de cueva estrecho te permite avanzar de este a oeste; un pasaje surge al sur."
data "Un tramo de cueva estrecho te permite avanzar de este a oeste; un pasaje surge al sur."
data "Un tramo de cueva estrecho te permite avanzar de oeste a sur."
data "Un tramo de cueva estrecho te permite avanzar de este a norte."
data "Un tramo de cueva estrecho te permite avanzar de este a oeste. Al norte y al sur surgen pasajes."
data "Un tramo de cueva estrecho te permite avanzar de este a oeste. Al norte surge un pasaje."
data "Un tramo de cueva estrecho te permite avanzar al oeste. Al norte surge un pasaje."
data "Refugio. Una amplia estancia de norte a este, hace de albergue a refugiados: hay banderas de ambos bandos. Un hombre anciano te contempla. Los refugiados te rodean."
data "Espiral. Cual escalera de caracol gigante, desciende a las profundidades, dejando a los refugiados al oeste."
data "Inicio de la espiral. Se eleva en la penumbra. La caverna se estrecha ahora como para una sola persona, hacia el este."
data "Puerta norte. En este pasaje grandes rocas se encuentran entre las columnas de un arco de medio punto."
data "Precipicio. El camino ahora no excede de dos palmos de cornisa sobre un abismo insondable. El soporte de roca gira en 'u' de oeste a sur."
data "Pasaje de salida. El paso se va haciendo menos estrecho a medida que se avanza hacia el sur, para entonces comenzar hacia el este."
data "Pasaje de gravilla. El paso se anchea de oeste a norte, y guijarros mojados y mohosos tachonan el suelo de roca."
data "Puente sobre el acueducto. Un puente se tiende de norte a sur sobre el curso del agua. Resbaladizas escaleras descienden hacia el oeste."
data "Remanso. Estruendosa corriente baja con el pasaje elevado desde el oeste, y forma un meandro arenoso. Unas escaleras suben al este."
data "Canal de agua. El agua baja del oeste con renovadas fuerzas, dejando un estrecho paso elevado lateral para avanzar a este o a oeste."
data "Gran Cascada. Cae el agua hacia el este, descendiendo con gran fuerza hacia el canal, no sin antes embalsarse en un lago poco profundo."
data "Interior de la cascada. Musgoso y rocoso, con la cortina de agua tras de ti, el nivel del agua ha crecido un poco en este curioso hueco."
data "Explanada. Una gran explanda enlosetada contempla un bello panorama de estalactitas. Unos casi imperceptibles escalones conducen al este."
data "Idolo. El Idolo parece un centinela siniestro de una gran roca que se encuentra al sur. Se puede volver a la explanada al oeste."
data "Pasaje estrecho. Como un pasillo que corteja el canal de agua, a su lado, baja de norte a sur. Se aprecia un aumento de luz hacia el sur."
data "Pasaje de la serpiente. El pasaje sigue de norte a sur."
data "Lago interior. Unas escaleras dan paso a un hermoso lago interior, y siguen hacia el oeste. Al norte, un oscuro y estrecho pasaje sube."
data "Cruce de pasajes. Estrechos pasos permiten ir al oeste, al este (menos oscuro), y al sur, un lugar de gran luminosidad."
data "Hogar de Ambrosio. Un catre, algunas velas y una mesa es todo lo que tiene Ambrosio."
data "Salida de la cueva. Por el oeste, una puerta impide, cuando cerrada, la salida de la cueva. Se adivina la luz diurna al otro lado."
data "Bosque a la entrada. Apenas se puede reconocer la entrada de la cueva, al este. El sendero sale del bosque hacia el oeste."
data "Sendero del bosque. El sendero recorre esta parte del bosque de este a oeste."
data "Camino norte. El camino norte de Westmorland se interna hacia el bosque, al norte (en tu estado no puedes ir), y a Westmorland, al sur."
data "Westmorland. La villa bulle de actividad con el mercado en el centro de la plaza, donde se encuentra el castillo."

rem n s e w u d
data 0,2,0,0,0,0
data 1,0,0,3,0,0
data 0,0,2,4,0,0
data 5,0,3,9,0,0
data 0,4,0,6,0,0
data 0,0,5,7,0,0
data 0,8,6,0,0,0
data 7,10,0,0,0,0
data 0,0,4,0,0,0
data 8,0,0,11,0,0
data 0,0,10,0,0,0
data 0,0,0,13,0,0
data 0,0,12,14,0,0
data 0,15,13,0,0,0
data 14,17,16,0,0,0
data 0,0,0,15,0,0
data 15,20,18,0,0,0
data 0,0,19,17,0,0
data 0,0,0,18,0,0
data 17,22,25,0,0,0
data 0,27,23,20,0,0
data 0,24,27,22,0,0
data 0,25,0,21,0,0
data 22,0,26,0,0,0
data 22,28,23,21,0,0
data 26,0,20,27,0,0
data 27,0,0,25,0,0
data 26,0,0,0,0,0
data 0,0,0,28,0,30
data 0,0,31,0,29,0
data 0,0,0,30,0,0
data 0,33,0,31,0,0
data 32,0,34,0,0,0
data 35,0,0,33,0,0
data 40,34,0,36,0,36
data 0,0,35,37,35,0
data 0,0,36,38,0,0
data 0,0,37,39,0,0
data 0,0,38,0,0,0
data 0,35,41,0,0,0
data 0,0,0,40,0,0
data 41,43,0,0,0,0
data 42,0,0,0,0,0
data 43,0,0,45,0,0
data 0,47,44,46,0,0
data 0,0,45,0,0,0
data 45,0,0,0,0,0
data 0,0,47,49,0,0
data 0,0,48,50,0,0
data 0,51,49,0,0,0
data 50,0,0,0,0,0


data "altar","ambrosio","antorcha","amorfa"
data "banderas","capa","cascada","derrumbe"
data "esmeralda","espada","harapo","hilo"
data "hombre","idolo","lago","llave"
data "pedernal","piedra","puerta","rocas"
data "serpiente","tronco","trozo"

data "Justo en la mitad del puente, debe sostener algo importante."
data "Ambrosio es un hombre de mediana edad, que te mira afable."
data "Apagada.","Ilumina perfectamente."
data "Son las banderas britana y sajona, respectivamente. Dos dragones rampantes, rojo y blanco, enfrentados.":rem respectivamente?!!!
data "Tu capa de general, de fina lana tintada de negro."
data "No ves nada por la cortina de agua. El lago es muy poco profundo."
data "Muchas, inalcanzables rocas, apiladas una sobre otra."
data "Es preciosa."
data "Legado de tu padre, fiel herramienta en mil batallas."
data "Un trozo un poco grande de capa."
data "Un hilo se ha desprendido al cortar la capa con la espada."
data "Es el jefe de los refugiados."
data "El ídolo tiene dos agujeros por ojos."
data "La luz entra por un resquicio, y caprichosos reflejos te maravillan."
data "Una llave grande, de hierro herrumboso."
data "Se trata de una dura y afilada piedra."
data "Recia y pesada, pero no muy grande, de forma piramidal."
data "Muy recia y con un gran candado."
data "Son muchas, aunque parecen ligeras y con huecos entre ellas."
data "Una serpiente bloquea el paso al sur, corriendo a su lado el agua."
data "Es un tronco recio, pero de liviano peso."
data "Es un poco de lo que antes era tu capa."

rem Loc Stc
data 18,1
data 19,2
data 255,0
data 255,0
data 28,1
data 0,0
data 38,1
data 9,1
data 39,0
data 0,0
data 255,0
data 255,0
data 28,1
data 41,1
data 44,1
data 46,1
data 255,0
data 18,0
data 47,1
data 31,1
data 43,1
data 15,0
data 255,0

data "abajo",0
data "abre",1
data "arriba",0
data "ataca",1
data "ayuda",0
data "bucea",0
data "coge",1
data "deja",1
data "este",0
data "examina",0
data "habla",1
data "mete",2
data "mira",0
data "norte",0
data "oeste",0
data "su",0:rem not "sur", because of the strange way the parser works
data "termina",0
data "tira",1

rem new system for the actions
rem not used yet, unfinished!!!

data 01,"abajo",0
data 01,"baja",0
data 01,"bajar",0
data 01,"descender",0
data 01,"desciende",0
data 02,"abre",1
data 02,"abrir",1
data 03,"arriba",0
data 03,"ascender",0
data 03,"asciende",0
data 03,"sube",0
data 03,"subir",0
data 04,"afila",1
data 04,"afilar",1
data 04,"ataca",1
data 04,"atacar",1
data 04,"corta",1
data 04,"cortar",1
data 04,"empuja",1
data 04,"empujar",1
data 04,"golpea",1
data 04,"golpear",1
data 04,"mata",1
data 04,"matar",1
data 04,"recorta",1
data 04,"recortar",1
data 04,"rompe",1
data 04,"romper",1
data 05,"ayuda",0
data 05,"ayudar",0
data 06,"bucea",0
data 06,"bucear",0
data 06,"nada",0
data 06,"nadar",0
data 07,"agarra",1
data 07,"agarrar",1
data 07,"coge",1
data 07,"coger",1
data 07,"toma",1
data 07,"tomar",1
data 08,"deja",1
data 08,"dejar",1
data 08,"soltar",1
data 08,"suelta",1
data 09,"e",0
data 09,"este",0
data 10,"ex",0
data 10,"examina",0
data 10,"examinar",0
data 10,"examinarte",0
data 10,"examínate",0
data 10,"i",0
data 11,"charla",1
data 11,"charlar",1
data 11,"conversa",1
data 11,"conversar",1
data 11,"habla",1
data 11,"hablar",1
data 12,"introduce",2
data 12,"introducir",2
data 12,"mete",2
data 12,"meter",2
data 12,"pon",2
data 12,"poner",2
data 13,"m",0
data 13,"mira",0
data 13,"mirar",0
data 13,"ojea",0
data 13,"ojear",0
data 14,"n",0
data 14,"norte",0
data 15,"o",0
data 15,"oeste",0
data 16,"s",0
data 16,"sur",0
data 17,"acaba",0
data 17,"acabar",0
data 17,"fin",0
data 17,"finaliza",0
data 17,"finalizar",0
data 17,"termina",0
data 17,"terminar",0
data 18,"arroja",1
data 18,"arrojar",1
data 18,"lanza",1
data 18,"lanzar",1
data 18,"tira",1
data 18,"tirar",1

rem --------------------------------------------
rem Debug

defproc _debug(text$)
  print "DEBUG POINT:"&text$
enddef

